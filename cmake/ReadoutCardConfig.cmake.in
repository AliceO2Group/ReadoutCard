@PACKAGE_INIT@

get_filename_component(ReadoutCard_CMAKE_DIR "${CMAKE_CURRENT_LIST_FILE}" PATH)
include(CMakeFindDependencyMacro)

set(Common_CMAKE_DIR @Common_DIR@)
set(InfoLogger_CMAKE_DIR @InfoLogger_ROOT@)

if(NOT APPLE)
  find_package(Python3 3.6 COMPONENTS Development)
  if(Python3_FOUND)
    set(boost_python_component "python${Python3_VERSION_MAJOR}${Python3_VERSION_MINOR}")
  else()
    # Backwards compatible. Can be removed once the Python3 recipe is stable
    message(WARNING "Python 3 was not found: falling back to Python 3")
    find_package(Python2 2.7 COMPONENTS Development REQUIRED)
    set(boost_python_component "python27")
  endif()
endif()

find_dependency(Common CONFIG HINTS ${Common_CMAKE_DIR})
find_dependency(InfoLogger CONFIG HINTS ${InfoLogger_CMAKE_DIR})

# find package must be used as Common already looks for boost and set Boost_FOUND
find_package(Boost QUIET
  COMPONENTS
  system
  filesystem
  ${boost_python_component}
  REQUIRED
)

if(NOT TARGET AliceO2::ReadoutCard)
  include("${ReadoutCard_CMAKE_DIR}/ReadoutCardTargets.cmake")
endif()

message(STATUS "ReadoutCard ${ReadoutCard_VERSION} found")
